#version 450

layout(local_size_x = 64) in; // workgroup size

layout(std430) restrict buffer edgeBuffer
{
    uint count;
    vec3 position[];
} edgeParticles;

layout(r32f, binding = 0) uniform restrict image3D distanceField;

const float MaxRadius = 0.08;
const float MinRadius = 0.00;//0.05 * distFieldSize;

const int boxSize = 6;//int(ceil(distFieldSize * MaxRadius / 2.0));

layout(location = 3) uniform int BoundaryType; // 0: Cube, 1: Sphere
layout(location = 4) uniform float BoundaryRadius; // normalized [0,1], sphere radius

bool inCube(vec3 p)
{
	return all(lessThan(p, vec3(1.0))) && all(greaterThanEqual(p, vec3(0.0)));
}

bool inSphere(vec3 p)
{
	vec3 c = vec3(0.5, 0.5, 0.5);
	return distance(p, c) <= BoundaryRadius;
}

bool inBoundary(vec3 p)
{
	return (BoundaryType == 0) ? inCube(p) : inSphere(p);
}

void main()
{
	uint id =
		gl_GlobalInvocationID.x * gl_WorkGroupSize.y * gl_WorkGroupSize.z +
		gl_GlobalInvocationID.y * gl_WorkGroupSize.z +
		gl_GlobalInvocationID.z;

	if(id < edgeParticles.count)
	{
		const ivec3 distFieldSize = imageSize(distanceField);
		vec3 pos = ((edgeParticles.position[id] + 1.0) / 2.2 + vec3(0.05, 0.05, 0.05)) * distFieldSize;
		vec3 center = pos;
		for(int x = -1 * boxSize; x < boxSize; ++x)
		{
			if(center.x + x < 0 || center.x + x >= distFieldSize.x)
				continue;

			for(int y = -1 * boxSize; y < boxSize; ++y)
			{
				if(center.y + y < 0 || center.y + y >= distFieldSize.y)
					continue;

				for(int z = -1 * boxSize; z < boxSize; ++z)
				{
					if(center.z + z < 0 || center.z + z >= distFieldSize.z)
						continue;

					ivec3 sampleCoord = ivec3(center) + ivec3(x, y, z);
					vec3 sampleNorm = vec3(sampleCoord) / vec3(distFieldSize);
					if(!inBoundary(sampleNorm))
						continue;

					float dist = distance(pos, vec3(sampleCoord)) / distFieldSize.x;
					if(dist >= MinRadius && dist <= MaxRadius)
					{
						//imageAtomicMin(distanceField, ivec3(center + ivec3(x, y, z)), int(dist * 10000));
						float prev = imageLoad(distanceField, sampleCoord).r;
						if(dist < prev)
							imageStore(distanceField, sampleCoord, vec4(dist));
					}
				}
			}
		}

	}
}
